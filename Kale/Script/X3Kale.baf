//Approval Shifts 

IF 
InParty(Myself)
Switch("X3KaleAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@300109)
RESPONSE #2
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@300109)
RESPONSE #3
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@300106)
RESPONSE #4
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@300106)
RESPONSE #5
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@300103)
RESPONSE #6
SetGlobal("X3KaleAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@300113)
RESPONSE #8
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@300116)
RESPONSE #9
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@300116)
RESPONSE #10
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@300119)
RESPONSE #11
SetGlobal("X3KaleAppChange","GLOBAL",0)
IncrementGlobal("X3KaleApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@300119)
END 

// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3KaleAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3KaleAppChange","GLOBAL",0)
END

//Reset to 120 if past max/reward bonus stats.
IF 
GlobalGT("X3KaleApp","GLOBAL",120)
InParty(Myself)
Switch("X3KaleAppMaxBonus","LOCALS")
THEN RESPONSE #0
SetGlobal("X3KaleApp","GLOBAL",120)
SetGlobal("X3KaleAppMaxBonus","LOCALS",1)
ChangeStat(Myself,DEX,1,ADD)
DisplayStringNoNameDlg(Player1,@30099)
RESPONSE #1 
SetGlobal("X3KaleApp","GLOBAL",120)
END 

IF 
GlobalLT("X3KaleApp","GLOBAL",-39)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END


// SEX CHANGE
IF
    InParty(Myself)
	Gender(Myself,MALE)
	HasItemEquiped("belt05",Myself)
    Global("X3KaleSexChange","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3KaleSexChange","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Quest "Reminder" Setup
IF 
	InParty(Myself)
	Global("X3KaleReminderTalk","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobalTimer("X3KaleReminderTimer","GLOBAL",SEVEN_DAYS) 
	SetGlobal("X3KaleReminderTalk","GLOBAL",1)       
END
// Quest "Reminder" Firing 
IF 
    InParty(Myself)
    Global("X3KaleMomTalk","GLOBAL",0)
    Global("X3KaleReminderTalk","GLOBAL",1)		
	GlobalTimerExpired("X3KaleReminderTimer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!AreaType(DUNGEON)
THEN
RESPONSE #100	
	Wait(1)
	SetGlobal("X3KaleReminderTalk","GLOBAL",2)
	StartDialogueNoSet(Player1)
END 
// Talk 3	
IF 
    InParty(Myself)
	!AreaCheck("%bg1_eet_symbol%4013")
    Global("X3KaleMomTalk","GLOBAL",2)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!AreaType(DUNGEON)
	THEN
  RESPONSE #100
	Wait(1)
	SetGlobal("X3KaleMomTalk","GLOBAL",3)
	StartDialogueNoSet(Player1)
END 
/* This sets the initial timer. REMEMBER the reseting of the timer must happen in the condition of the Dialogue file, not here. */
IF 
InParty(Myself)
Global("X3KaleTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobalTimer("X3KaleFirstTimer","GLOBAL",600) // TESTING will have this set at a VERY low number. 
SetGlobal("X3KaleTalk","GLOBAL",1)       // Each block must run only once.
END
// This is for the first talk's expired timer, which is a game timer of one day.
IF
InParty(Myself)
GlobalTimerExpired("X3KaleFirstTimer","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          
!See([ENEMY])
!AreaType(DUNGEON)			                               
Global("X3KaleTalk","GLOBAL",1)


THEN
RESPONSE #100
IncrementGlobal("X3KaleTalk","GLOBAL",1)
END

IF
InParty(Myself)
RealGlobalTimerExpired("X3KaleTimer","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          
!See([ENEMY])
!AreaType(DUNGEON)			  
OR(3)                       // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD          
Global("X3KaleTalk","GLOBAL",3)
Global("X3KaleTalk","GLOBAL",5)
Global("X3KaleTalk","GLOBAL",7)

THEN
RESPONSE #100
IncrementGlobal("X3KaleTalk","GLOBAL",1)
END



// So, if timer expired, everybody's able to talk and there's no battle, we increase the variable by one. Now our X3KaleTalk variable is at 2, or 4, or 6... or 20, or 30.
// EXACTLY like the condition in the beginning of each talk, right?
// Now all that's left is making Branwen talk to you. We could just add StartDialogueNoSet(Player1) into the last block, but the engine is tricky: if you give a command when Branwen's about to talk, she may miss her dialogue!
// So we add another block. 
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(4)                                    // YOU WILL NEED TO ADD +1 TO THIS OR NUMBER FOR EVERY ADDITONAL TALK YOU ADD  
Global("X3KaleTalk","GLOBAL",2)
Global("X3KaleTalk","GLOBAL",4)
Global("X3KaleTalk","GLOBAL",6)
Global("X3KaleTalk","GLOBAL",8)
THEN
RESPONSE #100
PlaySong(0)
StartDialogueNoSet(Player1)
END

// Cloak Timer
IF 
PartyHasItem("X3KTBQ")
Global("X3KaleCloakStart","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3KaleCloakStart","GLOBAL",1) 
END 

IF 
Global("X3KaleCloakStart","GLOBAL",1)
!AreaCheck("%bg1_eet_symbol%1320")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("X3KaleCloakStart","GLOBAL",2)
StartDialogueNoSet(Player1)
END

IF 
Global("X3KaleCloakStart","GLOBAL",3)
THEN
RESPONSE #100
SetGlobalTimer("X3KaleCloakMaking","GLOBAL",ONE_DAY) // One Game Day
SetGlobal("X3KaleCloakStart","GLOBAL",4) 
END 

IF 
Global("X3KaleCloak","GLOBAL",0)
GlobalTimerExpired("X3KaleCloakMaking","GLOBAL")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
THEN 
RESPONSE #100 
SetGlobal("X3KaleCloak","GLOBAL",1)
TakeItemReplace("X3KCLOG","X3KTBQ","X3Kale")
END 

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
Global("X3KaleCloak","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF 
Dead("X3KKEEP")
Global("X3KKEEPDEAD","GLOBAL",0)
!Global("X3KaleCloakQuestAccept","GLOBAL",2)
THEN
RESPONSE #100 
SetGlobal("X3KKEEPDEAD","GLOBAL",1)
DisplayString(Player1,@574)
AddJournalEntry(@10036,QUEST)
GiveItemCreate("X3KTBQ","X3Kale",0,0,0)
END 


// Talk 6 
/* As long as the player is out of the district post quest, this conversation will fire.*/
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          
!See([ENEMY])
!AreaType(DUNGEON)	
Global("X3KaleSixthTalk","GLOBAL",0)
GlobalGT("X3KaleQuestDone","GLOBAL",1)
!AreaCheck("%bg1_eet_symbol%0800")
!AreaCheck("%bg1_eet_symbol%0801")
!AreaCheck("%bg1_eet_symbol%0802")
!AreaCheck("%bg1_eet_symbol%0803")
!AreaCheck("%bg1_eet_symbol%0804")
!AreaCheck("%bg1_eet_symbol%0805")
!AreaCheck("%bg1_eet_symbol%0806")
!AreaCheck("%bg1_eet_symbol%0807")
!AreaCheck("%bg1_eet_symbol%0808")
!AreaCheck("%bg1_eet_symbol%0809")
!AreaCheck("%bg1_eet_symbol%0810")
!AreaCheck("%bg1_eet_symbol%0811")
!AreaCheck("%bg1_eet_symbol%0812")
!AreaCheck("%bg1_eet_symbol%0813")
!AreaCheck("%bg1_eet_symbol%0814")
THEN 
RESPONSE #100 
SetGlobal("X3KaleSixthTalk","GLOBAL",1)
PlaySong(0)
StartDialogueNoSet(Player1)
END

IF 
GlobalGT("Chapter","GLOBAL",5)

Global("X3KaleSixthTalk","GLOBAL",2)
Global("X3KaleFinalTalk","GLOBAL",0)
	InParty(Myself)
THEN 
RESPONSE #100 
	RealSetGlobalTimer("X3KaleFinalTimer","GLOBAL",3300) 
	SetGlobal("X3KaleFinalTalk","GLOBAL",1) 
	
END 

IF 
    InParty(Myself)
    Global("X3KaleFinalTalk","GLOBAL",1)		
	RealGlobalTimerExpired("X3KaleFinalTimer","GLOBAL")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!AreaType(DUNGEON)
THEN
RESPONSE #100	
	PlaySong(0)
	SetGlobal("X3KaleFinalTalk","GLOBAL",2) 
	StartDialogueNoSet(Player1)
END 

// Durlag's Tower  (AR0500 )
IF 
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%0500")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	Global("X3KaleTower","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3KaleTower","GLOBAL",1)
END	

// Wolfwere Caverns (AR2012)

IF 
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%2012")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	Global("X3KaleCaverns","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3KaleCaverns","GLOBAL",1)
END	

// Candlekeep (AR2626)
IF 
	InParty(Myself)
	AreaCheck("%bg1_eet_symbol%2626")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	Global("X3KaleKeep","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3KaleKeep","GLOBAL",1)
END	

// AR4500 
IF 
	AreaCheck("%bg1_eet_symbol%4500")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	Global("X3KaleFire","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3KaleFire","GLOBAL",1)
END	

//AR5201  
IF 
	AreaCheck("%bg1_eet_symbol%5201")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	Global("X3KaleRuins","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("X3KaleRuins","GLOBAL",1)
END	

IF 
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)    
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)                          
	!See([ENEMY])	
	OR(5)
	Global("X3KaleRuins","GLOBAL",1)
	Global("X3KaleFire","GLOBAL",1)
	Global("X3KaleKeep","GLOBAL",1)
	Global("X3KaleCaverns","GLOBAL",1)
	Global("X3KaleTower","GLOBAL",1)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END		

// These blocks set up Kale's experience based off of the PC's level.

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPLT(Player1,2000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,1999)
	XPLT(Player1,4000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	ChangeStat(Myself,XP,2000,SET)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,3999)
	XPLT(Player1,8000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	ChangeStat(Myself,XP,4000,SET)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,7999)
	XPLT(Player1,16000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	ChangeStat(Myself,XP,8000,SET)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,15999)
	XPLT(Player1,32000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	ChangeStat(Myself,XP,16000,SET)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,31999)
	XPLT(Player1,64000)
THEN RESPONSE #100
	SetInterrupt(FALSE)
	ChangeStat(Myself,XP,32000,SET)
	SetGlobal("BD_JOINXP","LOCALS",1)
	SetInterrupt(TRUE)
END

